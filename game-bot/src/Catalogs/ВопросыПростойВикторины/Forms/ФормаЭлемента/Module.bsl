#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	Картинка = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "Картинка");

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если КартинкаИзменена Тогда
		Если ЗначениеЗаполнено(Картинка) Тогда
			ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Картинка));
		Иначе
			ТекущийОбъект.Картинка = Неопределено;
		КонецЕсли;
		КартинкаИзменена = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбратьНовуюКартинку();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	Картинка = "";
	КартинкаИзменена = Истина;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ВыбратьНовуюКартинку()

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выберите картинку";
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Фильтр = "Формат PNG|*.png; |Формат JPEG|*.jpg;*.jpeg|";

	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	Если Результат <> Неопределено Тогда
		Картинка = Результат.Адрес;
		КартинкаИзменена = Истина;
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти